import com.vanniktech.maven.publish.SonatypeHost

plugins {
    alias(libs.plugins.com.android.library)
    alias(libs.plugins.org.jetbrains.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.com.vanniktech)
}

android {
    namespace 'com.hubtel.sdk.checkout'
    compileSdk 33

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        compose true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01, true)
    signAllPublications()

    coordinates("com.hubtel", "sdk.checkout", findProperty("versionName") ? findProperty("versionName") : "1.0.0-SNAPSHOT")

    pom {
        name = "Android-Hubtel-Library"
        description = "The Kotlin Android Checkout Library is a convenient and easy-to-use library that simplifies the process of implementing a checkout flow in your Android application.\n" +
                "It provides a set of classes and methods to help you create and manage a checkout intent, handle the checkout result, and retrieve important information about the transaction."
        url = "https://github.com/hubtel/hubtel-mobile-android-checkout-sdk"
        licenses {
            license {
                name = "MIT License"
                url = "http://www.opensource.org/licenses/mit-license.php"
            }
        }
        developers {
            developer {
                id = "albanybuipe96"
                name = "Albany Buipe"
                email = "mohammedarahman96@gmail.com"
            }
        }
        scm {
            connection = "scm:git:git:github.com/hubtel/hubtel-mobile-android-checkout-sdk.git"
            developerConnection = "git@github.com:hubtel/hubtel-mobile-android-checkout-sdk.git"
            url = "https://github.com/hubtel/hubtel-mobile-android-checkout-sdk.git"
        }
    }
}

dependencies {

    implementation libs.core.ktx
    implementation platform(libs.kotlin.bom)
    implementation libs.appcompat
    implementation libs.material
    implementation libs.core.ui
    implementation libs.core.utils

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.espresso.core

    // Room
    implementation libs.room.runtime
    implementation libs.room.ktx
    ksp libs.room.compiler

    implementation project(':core-network')
    implementation project(':core-storage')
}